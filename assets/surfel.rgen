#version 460
#include "standard.glsl"

layout(location = 0) rayPayloadEXT struct RayPayload {
    uint32_t surfel_index;
} payload;


void main() {
    SurfelEntry entry = s_surfel_pool.entries[gl_LaunchIDEXT.x];
    if (entry.direction >= 6) { // invalid entry
        return;
    }

    vec3 origin = vec3(entry.position) + vec3(0.5); // world space

    vec3 normal = faceId2Normal(uint8_t(entry.direction)); // world space

    
    uvec2 noiseSampleLocation;
    uint width = textureSize(blue_noise[1], 0).x;
    noiseSampleLocation.y = gl_LaunchIDEXT.x / width;
    noiseSampleLocation.x = gl_LaunchIDEXT.x - noiseSampleLocation.y * width;

    // Location on the patch
    vec3 offset = texelFetch(blue_noise[1], ivec2((noiseSampleLocation + uvec2(71, 83) + pushConstants.rand) % textureSize(blue_noise[1], 0)), 0).xyz;
    offset.z = 0.5;
    offset.xy -= 0.5;
    offset = rotateVectorByNormal(normal, offset);
    origin += offset;

    // Outgoing direction
    vec3 noiseSample = texelFetch(blue_noise[5], (ivec2(noiseSampleLocation + uvec2(16, 47) + pushConstants.rand) % textureSize(blue_noise[5], 0)), 0).xyz;
    // noiseSample is weighted on the z axis
    noiseSample = rotateVectorByNormal(normal, noiseSample);
    

    // Shoot shadow ray
    traceRayEXT(
        accelerationStructure,
        gl_RayFlagsOpaqueEXT, // RayFlags
        0xFF, // CullMask
        3, // SBT offset, ray type index // Use the same intersection shader. We need higher-quality intersection for shadow rays as well.
        4, // SBT stride, number of ray types // TODO: Make this a shader constant
        3, // missIndex
        origin * 4.0,     // ray origin
        0.1,           // ray min range. If we set this to 0.0, VK_DEVICE_LOST. Time stuck: 2 days
        noiseSample, // outgoing direction: cosine weighted on the normal direction
        10000.0, // tmax
        0 // payload
    );
    // TODO: stocastically caste rays directly to light sources also
}
